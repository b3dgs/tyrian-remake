plugins {
    id 'com.android.application'
}

group = 'com.b3dgs.tyrian'
version = '0.4.1'

android {
    packagingOptions {
        jniLibs {
            excludes += ['**/tyrian/**']
        }
        resources {
            excludes += ['**/tyrian/**']
        }
    }

    signingConfigs {
        app {
            storeFile file(property('keystore'))
            keyAlias property('keystore.alias')
            keyPassword property('keystore.password')
            storePassword property('keystore.storepassword')
        }
    }
    compileSdk 34
    defaultConfig {
        applicationId "com.b3dgs.tyrian"
        minSdkVersion 21
        targetSdkVersion 34
        signingConfig signingConfigs.app
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.app
        }
        debug {
            signingConfig signingConfigs.app
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets/', '../../tyrian-assets/src/main/resources/']
            jniLibs.srcDir 'jniLibs'
        }
    }
    namespace 'com.b3dgs.tyrian'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    //noinspection GradleDependency
    implementation 'net.java.dev.jna:jna:5.12.1@aar'
    implementation 'com.b3dgs:b3dgs-parent:2.0.0-SNAPSHOT'
    implementation 'com.b3dgs.lionengine:lionengine-core:10.0.0-SNAPSHOT'
    implementation 'com.b3dgs.lionengine:lionengine-core-android:10.0.0-SNAPSHOT'
    implementation 'com.b3dgs.lionengine:lionengine-audio-adlmidi:10.0.0-SNAPSHOT'
    implementation 'com.b3dgs.tyrian:tyrian-game:0.4.1-SNAPSHOT'
}
